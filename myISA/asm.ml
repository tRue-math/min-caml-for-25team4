(* 2オペランドではなく3オペランドのx86アセンブリもどき *)

type id_or_imm = V of Id.t | C of int
type t = { v: node; pos: Lexing.position }(* 命令の列 (caml2html: sparcasm_t) *)
and node =
  | Ans of exp
  | Let of (Id.t * Type.t) * exp * t
and exp = (* 一つ一つの命令に対応する式 (caml2html: sparcasm_exp) *)
  | Nop
  | Set of int
  | SetL of Id.l
  | Mov of Id.t
  | Neg of Id.t
  | Add of Id.t * id_or_imm
  | Sub of Id.t * id_or_imm
  | Ld of Id.t * int
  | St of Id.t * Id.t * int
  | FMov of Id.t
  | FNeg of Id.t
  | FAdd of Id.t * Id.t
  | FSub of Id.t * Id.t
  | FMul of Id.t * Id.t
  | FDiv of Id.t * Id.t
  | LdF of Id.t * int
  | StF of Id.t * Id.t * int
  | Comment of string
  (* virtual instructions *)
  | LdArray of Id.t * Id.t
  | StArray of Id.t * Id.t * Id.t
  | LdFArray of Id.t * Id.t
  | StFArray of Id.t * Id.t * Id.t
  | IfEq of Id.t * id_or_imm * t * t
  | IfLE of Id.t * id_or_imm * t * t
  | IfGE of Id.t * id_or_imm * t * t
  | IfFEq of Id.t * Id.t * t * t
  | IfFLE of Id.t * Id.t * t * t
  (* closure address, integer arguments, and float arguments *)
  | CallCls of Id.t * Id.t list * Id.t list
  | CallDir of Id.l * Id.t list * Id.t list
  | Save of Id.t * Id.t (* レジスタ変数の値をスタック変数へ保存 (caml2html: sparcasm_save) *)
  | Restore of Id.t (* スタック変数から値を復元 (caml2html: sparcasm_restore) *)
type fundef = { name : Id.l; args : Id.t list; fargs : Id.t list; body : t; ret : Type.t }
(* プログラム全体 = 浮動小数点数テーブル + トップレベル関数 + メインの式 (caml2html: sparcasm_prog) *)
type prog = Prog of (Id.l * float) list * fundef list * t

let fletd(x, e1, {v=e2;pos}) = {v=Let((x, Type.Float), e1, {v=e2;pos});pos}
let seq(e1, {v=e2;pos}) = {v=Let((Id.gentmp Type.Unit, Type.Unit), e1, {v=e2;pos});pos}

let regs = Array.init 16 (fun i -> Printf.sprintf "%%x%02d" (i+1))
let fregs = Array.init 32 (fun i -> Printf.sprintf "%%xmm%02d" i)
let allregs = Array.to_list regs
let allfregs = Array.to_list fregs
let reg_cl = regs.(Array.length regs - 1) (* closure address (caml2html: sparcasm_regcl) *)
let reg_sw = "%tmp" (* temporary for swap *)
let reg_fsw = "%ftmp" (* temporary for swap *)
let reg_sp = "%rsp" (* stack pointer *)
let reg_hp = "%rhp" (* heap pointer (caml2html: sparcasm_reghp) *)
let reg_ra = "%ra" (* return address *)
let is_reg x = x.[0] = '%'

(* super-tenuki *)
let rec remove_and_uniq xs = function
  | [] -> []
  | x :: ys when S.mem x xs -> remove_and_uniq xs ys
  | x :: ys -> x :: remove_and_uniq (S.add x xs) ys

(* free variables in the order of use (for spilling) (caml2html: sparcasm_fv) *)
let fv_id_or_imm = function V(x) -> [x] | _ -> []
let rec fv_exp = function
  | Nop | Set(_) | SetL(_) | Comment(_) | Restore(_) -> []
  | Mov(x) | Neg(x) | Ld(x, _) | LdF(x, _) | FMov(x) | FNeg(x) | Save(x, _) -> [x]
  | Add(x, y') | Sub(x, y') -> x :: fv_id_or_imm y'
  | FAdd(x, y) | FSub(x, y) | FMul(x, y) | FDiv(x, y) | St(x, y, _) | StF(x, y, _) -> [x; y]
  | IfEq(x, y', e1, e2) | IfLE(x, y', e1, e2) | IfGE(x, y', e1, e2) -> x :: fv_id_or_imm y' @ remove_and_uniq S.empty (fv e1 @ fv e2) (* uniq here just for efficiency *)
  | IfFEq(x, y, e1, e2) | IfFLE(x, y, e1, e2) -> x :: y :: remove_and_uniq S.empty (fv e1 @ fv e2) (* uniq here just for efficiency *)
  | CallCls(x, ys, zs) -> x :: ys @ zs
  | CallDir(_, ys, zs) -> ys @ zs
and fv {v=e;_} = match e with
  | Ans(exp) -> fv_exp exp
  | Let((x, t), exp, e) ->
      fv_exp exp @ remove_and_uniq (S.singleton x) (fv e)
let fv e = remove_and_uniq S.empty (fv e)

let rec concat {v=e1;pos} xt e2 =
  let set_pos e = {v=e; pos} in
  match e1 with
  | Ans(exp) -> set_pos (Let(xt, exp, e2))
  | Let(yt, exp, e1') -> set_pos (Let(yt, exp, concat e1' xt e2))

let align i = (if i mod 8 = 0 then i else i + 4)
